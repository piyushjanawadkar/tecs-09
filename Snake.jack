class Snake {

  field Body body;

  constructor Snake new(int snakeLength, int snakeMaxLength) {
    let body = Body.new(snakeLength, snakeMaxLength, 10, Direction.right());
    return this;
  }

  method void draw() {
    var Cell cell;
    var Rectangle rect;
    let cell = body.firstCell();
    while (~(cell = null)) {
      let rect = cell.rect();
      do rect.draw();
      let cell = body.nextCell(cell);
    }
    return;
  }

  method void setPosition(int x, int y) {
    do body.shiftTo(x, y);
    return;
  }

  method void move(Direction direction, int durationMillis) {
    var Rectangle tip;
    var Rectangle end;

    do body.extendTip(direction);
    let tip = body.tip();
    do tip.draw();
    let end = body.end();
    do end.erase();
    do body.trimEnd();

    do Sys.wait(durationMillis);
    return;
  }
}
