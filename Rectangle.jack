class Rectangle {

  field Point topLeft;
  field int size;

  constructor Rectangle new(Point argTopLeft, int argSize) {
    let topLeft = argTopLeft;
    let size = argSize;
    return this;
  }

  method Point topLeft() {
    return topLeft;
  }

  method Point bottomRight() {
    var int span;
    let span = size - 1;
    return Point.new(topLeft.x() + span, topLeft.y() + span);
  }

  method int size() {
    return size;
  }

  method void draw(Color color) {
    var Point bottomRight;
    let bottomRight = bottomRight();

    do Screen.setColor(color.value());    
    do Screen.drawRectangle(
        topLeft.x(), topLeft.y(),
        bottomRight.x(), bottomRight.y());

    return;
  }

  method boolean equals(Rectangle other) {
    var Point otherTopLeft;

    let otherTopLeft = other.topLeft();
    return topLeft.equals(otherTopLeft);
  }

  method void printString() {
    var Point bottomRight;

    do Output.printString("[");
    do topLeft.printString();

    do Output.printString(", ");

    let bottomRight = bottomRight();
    do bottomRight.printString();
    do Output.printString("]");

    return;
  }
}
