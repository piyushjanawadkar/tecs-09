class Rectangle {

  field Point topLeft;
  field int size;
  field Point bottomRight;

  constructor Rectangle new(Point argTopLeft, int argSize) {
    var int span;

    let topLeft = argTopLeft;
    let size = argSize;

    let span = size - 1;
    let bottomRight = Point.new(topLeft.x() + span, topLeft.y() + span);

    return this;
  }

  method void dispose() {
    do topLeft.dispose();
    do bottomRight.dispose();
    do Memory.deAlloc(this);
    return;
  }

  method Point topLeft() {
    return topLeft;
  }

  method Point bottomRight() {
    return bottomRight;
  }

  method int size() {
    return size;
  }

  method void draw(Color color) {
    do Screen.setColor(color.value());
    do Screen.drawRectangle(
        topLeft.x(), topLeft.y(),
        bottomRight.x(), bottomRight.y());

    return;
  }

  method boolean equals(Rectangle other) {
    var Point otherTopLeft;

    let otherTopLeft = other.topLeft();
    return topLeft.equals(otherTopLeft);
  }

  method void print() {
    do Output.printString("[");
    do topLeft.printString();

    do Output.printString(", ");

    do bottomRight.printString();
    do Output.printString("]");

    return;
  }
}
