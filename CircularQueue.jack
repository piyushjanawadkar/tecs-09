class CircularQueue {

  field Array buffer;
  field int bufferSize;
  field int front;
  field int back;
  field int length;

  constructor CircularQueue new(int argMaxNumCells) {
    let bufferSize = argMaxNumCells;
    let buffer = Array.new(bufferSize);
    do resetIndices();
    let length = 0;
    return this;
  }

  method void dispose() {
    do Memory.deAlloc(buffer);
    let bufferSize = 0;
    let length = 0;
    do resetIndices();
    return;
  }

  method boolean isEmpty() {
    return front < 0;
  }

  method void enqueue(Rectangle rect) {
    var Cell cell;

    let front = advanceIndex(front);
    let cell = Cell.new(rect, front);
    
    let buffer[front] = cell;
    let length = length + 1;

    if (length = 1) {
      let back = advanceIndex(back);
    }
    return;
  }

  method void dequeue() {
    let back = advanceIndex(back);
    let length = length - 1;

    if (length = 0) {
      do resetIndices();
    }
    return;
  }

  method Cell front() {
    if (~isEmpty()) {
      return buffer[front];
    }
    return null;
  }

  method Cell back() {
    if (~isEmpty()) {
      return buffer[back];
    }
    return null;
  }

  method Cell nextCell(Cell cell) {
    var int index;
    let index = cell.index();
    if (index = front) {
      return null;
    }

    let index = advanceIndex(index);
    return buffer[index];
  }

  method int advanceIndex(int index) {
    let index = index + 1;
    if (index = bufferSize) {
      let index = 0;
    }
    return index;
  }

  method void resetIndices() {
    let front = -1;
    let back = -1;
    return;
  }

  method boolean intersect(Rectangle rect) {
    var Cell cell;
    var Rectangle cellRect;

    let cell = back();
    while (~(cell = null)) {
      let cellRect = cell.rect();
      if (rect.equals(cellRect)) {
        return true;
      }
      let cell = nextCell(cell);
    }

    return false; 
  }
}
